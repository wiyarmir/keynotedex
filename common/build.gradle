apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'org.jetbrains.kotlin.native.cocoapods'
apply plugin: 'co.touchlab.kotlinxcodesync'

version = keynotedexVersion

kotlin {
    targets {
        def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'ios') {
            compilations.main.outputKinds("FRAMEWORK")
            compilations.test.outputKinds("FRAMEWORK")
        }
    }

    jvm {

    }

    js {
        compileKotlinJs {
            kotlinOptions.metaInfo = true
            kotlinOptions.sourceMap = true
            kotlinOptions.moduleKind = "commonjs"
            kotlinOptions.main = "call"
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                api Libs.kotlinStdlibCommon
                api Libs.kotlinxCoroutinesCoreCommon
                api Libs.kotlinxSerializationRuntimeCommon
                api Libs.ktorClientCore
                api Libs.ktorClientAuth
                api Libs.ktorClientJson
                api Libs.ktorClientLogging
            }
        }

        commonTest {
            dependencies {
                api Libs.kotlinTestCommon
            }
        }

        iosMain {
            dependencies {
                api Libs.kotlinxCoroutinesCoreNative
                api Libs.kotlinxSerializationRuntimeNative
                api Libs.ktorClientIos
                api Libs.ktorClientJsonNative
                api Libs.ktorClientLoggingNative
            }
        }

        jvmMain {
            dependencies {
                api Libs.kotlinStdlib
                api Libs.kotlinxSerializationRuntime
                api Libs.ktorClientJsonJvm
                api Libs.ktorClientLoggingJvm
            }
        }

        jvmTest {
            dependencies {
                api Libs.junit
                api Libs.kotlinTestJunit
                api Libs.kotlinTest
            }
        }

        jsMain {
            dependencies {
                api Libs.kotlinStdlibJs
                api Libs.kotlinxSerializationRuntimeJs
                api Libs.ktorClientJs
                api Libs.ktorClientJsonJs
                api Libs.ktorClientLoggingJs
            }
        }

        jsTest {
            dependencies {
                api Libs.kotlinTestJs
            }
        }
    }

    cocoapods {
        summary = "Keynotedex common module"
        homepage = "https://github.com/wiyarmir/keynotedex"
    }
}

configurations {
    compileClasspath
}

xcode {
    projectPath = "../ios/Keynotedex.xcodeproj"
    target = "Keynotedex"
}


//noinspection GroovyAssignabilityCheck
task packForXCode(type: Sync) {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String mode = System.getenv('CONFIGURATION')?.toUpperCase() ?: 'DEBUG'

    inputs.property "mode", mode
    dependsOn kotlin.targets.ios.compilations.main.linkTaskName("FRAMEWORK", mode)

    from { kotlin.targets.ios.compilations.main.getBinary("FRAMEWORK", mode).parentFile }
    into frameworkDir

    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}
tasks.build.dependsOn packForXCode

//noinspection GroovyAssignabilityCheck
task packForXCodeTest(type: Sync) {
    final File frameworkDir = new File(buildDir, "xcode-frameworks-test")
    final String mode = System.getenv('CONFIGURATION')?.toUpperCase() ?: 'DEBUG'

    inputs.property "mode", mode
    dependsOn "linkTestDebugFrameworkIos"
    dependsOn kotlin.targets.ios.compilations.test.linkTaskName("FRAMEWORK", mode)

    from { kotlin.targets.ios.compilations.test.getBinary("FRAMEWORK", mode).parentFile }
    into frameworkDir

    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}

//noinspection GroovyAssignabilityCheck
task iosTest {
    def device = project.findProperty("iosDevice")?.toString() ?: "iPhone XR"
    dependsOn 'linkTestDebugExecutableIos'
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = "Runs tests for target 'ios' on an iOS simulator"

    doLast {
        def binary = kotlin.targets.ios.compilations.test.getBinary('EXECUTABLE', 'DEBUG')
        exec {
            commandLine 'xcrun', 'simctl', 'spawn', device, binary.absolutePath
        }
    }
}
detekt {
    input = files(
            "src/commonMain/kotlin",
            "src/iosMain/kotlin",
            "src/jsMain/kotlin",
            "src/jvmMain/kotlin",
    )
    
}